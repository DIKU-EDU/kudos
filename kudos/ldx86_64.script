/*                                            (for emacs: -*- c -*-)
 *  A linker script for KUDOS
 */

ENTRY(_start)
OUTPUT_FORMAT("elf64-x86-64")
phys = 0x00100000;
SECTIONS
{
    /*
    * Actual code
    */
    .text phys : AT(phys) {
        code = .;
        *(.boot*)
        *(.text*)
        *(.rodata*)
        . = ALIGN(4096);
    }
    
    /*
    * Kernel data
    */
    .data : AT(phys + (data - code))
    {
        data = .;
        *(.data*)
        PROVIDE(kernel_symbols_start = .);
        PROVIDE(kernel_symbols_end = .);
        . = ALIGN(4096);
    }
    
    /*
    * Statically defined, uninitialized values
    */
    .bss : AT(phys + (bss - code))
    {
        bss = .;
        *(.bss*)
        . = ALIGN(4096);
    }
    
    /*
    * End of kernel.
    */
    KERNEL_ENDS_HERE = .;
    
    /*
    * Get rid of unnecessary GCC bits.
    */
    /DISCARD/ :
    {
    *(.comment)
    *(.eh_frame)
    *(.note.gnu.build-id)
    }
}




